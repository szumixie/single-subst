{-# OPTIONS
  --without-K
  --prop
  --rewriting
  --confluence-check
  --hidden-argument-puns #-}

module TT.SSC.Properties where

open import TT.Lib
open import TT.SSC
open import TT.SSC.AlphaNf

infixl 2 _++_ _▹_
infixl 9 _∘_ _[_]ᵀ* _[_]ᵗ* _[_]ᵀˡ _[_]ᵀˡ*
infixl 10 _⁺* _⁺^ _⁺^*

private variable
  i i₀ i₁ : ℕ
  Γ Γ₀ Γ₁ Δ Δ₀ Δ₁ Θ Θ₀ Θ₁ : Con
  γ : Sub Δ Γ
  A A₀ A₁ B : Ty Γ i
  a a₀ a₁ b f α : Tm Γ A

data Sub* : Con → Con → Set where
  ⌜_⌝ : Sub Δ Γ → Sub* Δ Γ
  _∘_ : Sub* Δ Γ → Sub* Θ Δ → Sub* Θ Γ
  id : Sub* Γ Γ

_[_]ᵀ* : Ty Γ i → Sub* Δ Γ → Ty Δ i
A [ ⌜ γ ⌝ ]ᵀ* = A [ γ ]ᵀ
A [ γ* ∘ δ* ]ᵀ* = A [ γ* ]ᵀ* [ δ* ]ᵀ*
A [ id ]ᵀ* = A

_[_]ᵗ* : Tm Γ A → (γ* : Sub* Δ Γ) → Tm Δ (A [ γ* ]ᵀ*)
a [ ⌜ γ ⌝ ]ᵗ* = a [ γ ]ᵗ
a [ γ* ∘ δ* ]ᵗ* = a [ γ* ]ᵗ* [ δ* ]ᵗ*
a [ id ]ᵗ* = a

_⁺* : (γ* : Sub* Δ Γ) → Sub* (Δ ▹ A [ γ* ]ᵀ*) (Γ ▹ A)
⌜ γ ⌝ ⁺* = ⌜ γ ⁺ ⌝
(γ* ∘ δ*) ⁺* = γ* ⁺* ∘ δ* ⁺*
id ⁺* = id

private variable
  γ*₀ γ*₁ δ*₀ δ*₁ : Sub* Δ Γ

opaque
  unfolding coe

  ap-Sub* : Δ₀ ≡ Δ₁ → Γ₀ ≡ Γ₁ → Sub* Δ₀ Γ₀ ≡ Sub* Δ₁ Γ₁
  ap-Sub* refl refl = refl

  apᵈ-∘* :
    (Δ₀₁ : Δ₀ ≡ Δ₁) (Γ₀₁ : Γ₀ ≡ Γ₁) → γ*₀ ≡[ ap-Sub* Δ₀₁ Γ₀₁ ] γ*₁ →
    (Θ₀₁ : Θ₀ ≡ Θ₁) → δ*₀ ≡[ ap-Sub* Θ₀₁ Δ₀₁ ] δ*₁ →
    γ*₀ ∘ δ*₀ ≡[ ap-Sub* Θ₀₁ Γ₀₁ ] γ*₁ ∘ δ*₁
  apᵈ-∘* refl refl refl refl refl = refl

  ap-[]ᵀ* : A₀ ≡ A₁ → (γ* : Sub* Δ Γ) → A₀ [ γ* ]ᵀ* ≡ A₁ [ γ* ]ᵀ*
  ap-[]ᵀ* refl _ = refl

  apᵈ-[]ᵀ* :
    (Δ₀₁ : Δ₀ ≡ Δ₁) →
    γ*₀ ≡[ ap-Sub* Δ₀₁ refl ] γ*₁ → A [ γ*₀ ]ᵀ* ≡[ ap-Ty Δ₀₁ ] A [ γ*₁ ]ᵀ*
  apᵈ-[]ᵀ* refl refl = refl

  apᵈ-[]ᵗ* :
    (A₀₁ : A₀ ≡ A₁) → a₀ ≡[ ap-Tm A₀₁ ] a₁ → (γ* : Sub* Δ Γ) →
    a₀ [ γ* ]ᵗ* ≡[ ap-Tm (ap-[]ᵀ* A₀₁ γ*) ] a₁ [ γ* ]ᵗ*
  apᵈ-[]ᵗ* refl refl _ = refl

  apᵈ-[]ᵗ*ᵣ :
    {A : Ty Γ i} {a : Tm Γ A} →
    (Δ₀₁ : Δ₀ ≡ Δ₁) (γ₀₁ : γ*₀ ≡[ ap-Sub* Δ₀₁ refl ] γ*₁) →
    a [ γ*₀ ]ᵗ* ≡[ ap-Tm₂ Δ₀₁ (apᵈ-[]ᵀ* Δ₀₁ γ₀₁) ] a [ γ*₁ ]ᵗ*
  apᵈ-[]ᵗ*ᵣ refl refl = refl

p-⁺ᵀ* :
  (γ* : Sub* Δ Γ) →
  B [ p ]ᵀ [ γ* ⁺* ]ᵀ* ≡ B [ γ* ]ᵀ* [ p ]ᵀ ∈ Ty (Δ ▹ A [ γ* ]ᵀ*) i
p-⁺ᵀ* ⌜ γ ⌝ = p-⁺ᵀ
p-⁺ᵀ* (γ* ∘ δ*) = ap-[]ᵀ* (p-⁺ᵀ* γ*) (δ* ⁺*) ∙ p-⁺ᵀ* δ*
p-⁺ᵀ* id = refl

p-⁺ᵗ* :
  (γ* : Sub* Δ Γ) →
  b [ p ]ᵗ [ γ* ⁺* ]ᵗ* ≡[ ap-Tm (p-⁺ᵀ* γ*) ]
  b [ γ* ]ᵗ* [ p ]ᵗ ∈ Tm (Δ ▹ A [ γ* ]ᵀ*) (B [ γ* ]ᵀ* [ p ]ᵀ)
p-⁺ᵗ* ⌜ γ ⌝ = p-⁺ᵗ
p-⁺ᵗ* (γ* ∘ δ*) = apᵈ-[]ᵗ* (p-⁺ᵀ* γ*) (p-⁺ᵗ* γ*) (δ* ⁺*) ∙ᵈ p-⁺ᵗ* δ*
p-⁺ᵗ* id = reflᵈ

q-⁺* :
  (γ* : Sub* Δ Γ) →
  q [ γ* ⁺* ]ᵗ* ≡[ ap-Tm (p-⁺ᵀ* γ*) ]
  q ∈ Tm (Δ ▹ A [ γ* ]ᵀ*) (A [ γ* ]ᵀ* [ p ]ᵀ)
q-⁺* ⌜ γ ⌝ = q-⁺
q-⁺* (γ* ∘ δ*) = apᵈ-[]ᵗ* (p-⁺ᵀ* γ*) (q-⁺* γ*) (δ* ⁺*) ∙ᵈ q-⁺* δ*
q-⁺* id = reflᵈ

⟨⟩-[]ᵀ* :
  (γ* : Sub* Δ Γ) → B [ ⟨ a ⟩ ]ᵀ [ γ* ]ᵀ* ≡ B [ γ* ⁺* ]ᵀ* [ ⟨ a [ γ* ]ᵗ* ⟩ ]ᵀ
⟨⟩-[]ᵀ* ⌜ γ ⌝ = ⟨⟩-[]ᵀ
⟨⟩-[]ᵀ* (γ* ∘ δ*) = ap-[]ᵀ* (⟨⟩-[]ᵀ* γ*) δ* ∙ ⟨⟩-[]ᵀ* δ*
⟨⟩-[]ᵀ* id = refl

U-[]* : (γ* : Sub* Δ Γ) → U i [ γ* ]ᵀ* ≡ U i
U-[]* ⌜ γ ⌝ = U-[]
U-[]* (γ* ∘ δ*) = ap-[]ᵀ* (U-[]* γ*) δ* ∙ U-[]* δ*
U-[]* id = refl

El-[]* :
  (γ* : Sub* Δ Γ) → El α [ γ* ]ᵀ* ≡ El (coe (ap-Tm (U-[]* γ*)) (α [ γ* ]ᵗ*))
El-[]* ⌜ γ ⌝ = El-[]
El-[]* (γ* ∘ δ*) =
  ap-[]ᵀ* (El-[]* γ*) δ* ∙
  El-[]* δ* ∙
  ap-El (splitr (apᵈ-[]ᵗ* (sym (U-[]* γ*)) (splitl reflᵈ) δ*))
El-[]* id = ap-El (undep refl)

c-[]* : (γ* : Sub* Δ Γ) → c A [ γ* ]ᵗ* ≡[ ap-Tm (U-[]* γ*) ] c (A [ γ* ]ᵀ*)
c-[]* ⌜ γ ⌝ = c-[]
c-[]* (γ* ∘ δ*) = apᵈ-[]ᵗ* (U-[]* γ*) (c-[]* γ*) δ* ∙ᵈ c-[]* δ*
c-[]* id = reflᵈ

Π-[]* : (γ* : Sub* Δ Γ) → Π A B [ γ* ]ᵀ* ≡ Π (A [ γ* ]ᵀ*) (B [ γ* ⁺* ]ᵀ*)
Π-[]* ⌜ γ ⌝ = Π-[]
Π-[]* (γ* ∘ δ*) = ap-[]ᵀ* (Π-[]* γ*) δ* ∙ Π-[]* δ*
Π-[]* id = refl

app-[]* :
  (γ* : Sub* Δ Γ) →
  app f a [ γ* ]ᵗ* ≡[ ap-Tm (⟨⟩-[]ᵀ* γ*) ]
  app (coe (ap-Tm (Π-[]* γ*)) (f [ γ* ]ᵗ*)) (a [ γ* ]ᵗ*)
app-[]* ⌜ γ ⌝ = app-[]
app-[]* (γ* ∘ δ*) =
  apᵈ-[]ᵗ* (⟨⟩-[]ᵀ* γ*) (app-[]* γ*) δ* ∙ᵈ
  app-[]* δ* ∙ᵈ
  dep (ap-app (splitr (apᵈ-[]ᵗ* (sym (Π-[]* γ*)) (splitl reflᵈ) δ*)))
app-[]* id = dep (ap-app (undep refl))

lam-[]* :
  (γ* : Sub* Δ Γ) → lam b [ γ* ]ᵗ* ≡[ ap-Tm (Π-[]* γ*) ] lam (b [ γ* ⁺* ]ᵗ*)
lam-[]* ⌜ γ ⌝ = lam-[]
lam-[]* (γ* ∘ δ*) = apᵈ-[]ᵗ* (Π-[]* γ*) (lam-[]* γ*) δ* ∙ᵈ lam-[]* δ*
lam-[]* id = reflᵈ

data Tel (Γ : Con) : Set
_++_ : (Γ : Con) → Tel Γ → Con

data Tel Γ where
  ◇ : Tel Γ
  _▹_ : (Ω : Tel Γ) → Ty (Γ ++ Ω) i → Tel Γ

Γ ++ ◇ = Γ
Γ ++ (Ω ▹ A) = (Γ ++ Ω) ▹ A

private variable
  Ω Ω₀ Ω₁ : Tel Γ

_[_]ᵀˡ : Tel Γ → Sub Δ Γ → Tel Δ
_⁺^ : (γ : Sub Δ Γ) → Sub (Δ ++ Ω [ γ ]ᵀˡ) (Γ ++ Ω)

◇ [ γ ]ᵀˡ = ◇
(Ω ▹ A) [ γ ]ᵀˡ = Ω [ γ ]ᵀˡ ▹ A [ γ ⁺^ ]ᵀ

_⁺^ {Ω = ◇} γ = γ
_⁺^ {Ω = Ω ▹ A} γ = γ ⁺^ ⁺

_[_]ᵀˡ* : Tel Γ → Sub* Δ Γ → Tel Δ
Ω [ ⌜ γ ⌝ ]ᵀˡ* = Ω [ γ ]ᵀˡ
Ω [ γ* ∘ δ* ]ᵀˡ* = Ω [ γ* ]ᵀˡ* [ δ* ]ᵀˡ*
Ω [ id ]ᵀˡ* = Ω

_⁺^* : (γ* : Sub* Δ Γ) → Sub* (Δ ++ Ω [ γ* ]ᵀˡ*) (Γ ++ Ω)
⌜ γ ⌝ ⁺^* = ⌜ γ ⁺^ ⌝
(γ* ∘ δ*) ⁺^* = γ* ⁺^* ∘ δ* ⁺^*
id ⁺^* = id

opaque
  unfolding coe

  ap-++ : Ω₀ ≡ Ω₁ → (Γ ++ Ω₀) ≡ (Γ ++ Ω₁)
  ap-++ refl = refl

  ap-▹ᵀˡ :
    (Ω₀₁ : Ω₀ ≡ Ω₁) → A₀ ≡[ ap-Ty (ap-++ Ω₀₁) ] A₁ → (Ω₀ ▹ A₀) ≡ (Ω₁ ▹ A₁)
  ap-▹ᵀˡ refl refl = refl

  ▹ᵀˡ-inj-Ω : (Ω₀ ▹ A₀) ≡ (Ω₁ ▹ A₁) → Ω₀ ≡ Ω₁
  ▹ᵀˡ-inj-Ω refl = refl

  ▹ᵀˡ-inj-i :
    (Ω▹A₀₁ : (Ω₀ ▹ A₀ ∈ Ty (Γ ++ Ω₀) i₀) ≡ (Ω₁ ▹ A₁ ∈ Ty (Γ ++ Ω₁) i₁)) →
    i₀ ≡ i₁
  ▹ᵀˡ-inj-i refl = refl

  ▹ᵀˡ-inj-A :
    (Ω▹A₀₁ : (Ω₀ ▹ A₀) ≡ (Ω₁ ▹ A₁)) →
    A₀ ≡[ ap-Ty₂ (ap-++ (▹ᵀˡ-inj-Ω Ω▹A₀₁)) (▹ᵀˡ-inj-i Ω▹A₀₁) ] A₁
  ▹ᵀˡ-inj-A refl = refl

  ap-[]ᵀˡ* : Ω₀ ≡ Ω₁ → (γ* : Sub* Δ Γ) → Ω₀ [ γ* ]ᵀˡ* ≡ Ω₁ [ γ* ]ᵀˡ*
  ap-[]ᵀˡ* refl _ = refl

  apᵈ-⁺^* :
    (γ* : Sub* Δ Γ) (Ω₀₁ : Ω₀ ≡ Ω₁) →
    γ* ⁺^* ≡[ ap-Sub* (ap-++ (ap-[]ᵀˡ* Ω₀₁ γ*)) (ap-++ Ω₀₁) ] γ* ⁺^*
  apᵈ-⁺^* _ refl = refl

◇-[]ᵀˡ* : (γ* : Sub* Δ Γ) → ◇ [ γ* ]ᵀˡ* ≡ ◇
◇-[]ᵀˡ* ⌜ γ ⌝ = refl
◇-[]ᵀˡ* (γ* ∘ δ*) = ap-[]ᵀˡ* (◇-[]ᵀˡ* γ*) δ* ∙ ◇-[]ᵀˡ* δ*
◇-[]ᵀˡ* id = refl

▹-[]ᵀˡ* : (γ* : Sub* Δ Γ) → (Ω ▹ A) [ γ* ]ᵀˡ* ≡ (Ω [ γ* ]ᵀˡ* ▹ A [ γ* ⁺^* ]ᵀ*)
▹-[]ᵀˡ* ⌜ γ ⌝ = refl
▹-[]ᵀˡ* (γ* ∘ δ*) = ap-[]ᵀˡ* (▹-[]ᵀˡ* γ*) δ* ∙ ▹-[]ᵀˡ* δ*
▹-[]ᵀˡ* id = refl

⁺^*-◇ : (γ* : Sub* Δ Γ) → γ* ⁺^* ≡[ ap-Sub* (ap-++ (◇-[]ᵀˡ* γ*)) refl ] γ*
⁺^*-◇ ⌜ γ ⌝ = reflᵈ
⁺^*-◇ (γ* ∘ δ*) =
  apᵈ-∘*
    (ap-++ (◇-[]ᵀˡ* γ*))
    refl
    (⁺^*-◇ γ*)
    (ap-++ (ap-[]ᵀˡ* (◇-[]ᵀˡ* γ*) δ* ∙ ◇-[]ᵀˡ* δ*))
    (apᵈ-⁺^* δ* (◇-[]ᵀˡ* γ*) ∙ᵈ ⁺^*-◇ δ*)
⁺^*-◇ id = reflᵈ

⁺^*-▹ :
  (γ* : Sub* Δ Γ) →
  γ* ⁺^* ≡[ ap-Sub* (ap-++ (▹-[]ᵀˡ* γ*)) refl ]
  γ* ⁺^* ⁺* ∈ Sub* (Δ ++ Ω [ γ* ]ᵀˡ* ▹ A [ γ* ⁺^* ]ᵀ*) (Γ ++ Ω ▹ A)
⁺^*-▹ ⌜ γ ⌝ = reflᵈ
⁺^*-▹ (γ* ∘ δ*) =
  apᵈ-∘*
    (ap-++ (▹-[]ᵀˡ* γ*))
    refl
    (⁺^*-▹ γ*)
    (ap-++ (ap-[]ᵀˡ* (▹-[]ᵀˡ* γ*) δ* ∙ ▹-[]ᵀˡ* δ*))
    (apᵈ-⁺^* δ* (▹-[]ᵀˡ* γ*) ∙ᵈ ⁺^*-▹ δ*)
⁺^*-▹ id = reflᵈ

module _
  (γ*₀ γ*₁ : Sub* Δ Γ) (γ*₀₁ᵀ : ∀ {i} {A : Ty Γ i} → A [ γ*₀ ]ᵀ* ≡ A [ γ*₁ ]ᵀ*)
  where
  []ᵀ→⁺^ᵀᵛ :
    (Ω-γ*₀₁ : Ω [ γ*₀ ]ᵀˡ* ≡ Ω [ γ*₁ ]ᵀˡ*) → Var (Γ ++ Ω) A a →
    A [ γ*₀ ⁺^* ]ᵀ* ≡[ ap-Ty (ap-++ Ω-γ*₀₁) ] A [ γ*₁ ⁺^* ]ᵀ*
  []ᵀ→⁺^ᵀᵛ {Ω = ◇} ◇-γ*₀₁ aᵛ =
    apᵈ-[]ᵀ* (ap-++ (◇-[]ᵀˡ* γ*₀)) (⁺^*-◇ γ*₀) ∙ᵈ
    dep γ*₀₁ᵀ ∙ᵈ
    apᵈ-[]ᵀ* (ap-++ (sym (◇-[]ᵀˡ* γ*₁))) (symᵈ (⁺^*-◇ γ*₁))
  []ᵀ→⁺^ᵀᵛ {Ω = Ω ▹ A} Ω▹A-γ*₀₁ vz =
    apᵈ-[]ᵀ* (ap-++ (▹-[]ᵀˡ* γ*₀)) (⁺^*-▹ γ*₀) ∙ᵈ
    dep (p-⁺ᵀ* (γ*₀ ⁺^*)) ∙ᵈ
    apᵈ-[]ᵀ
      (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′))
      (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′)
      (ap-▹ (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′)) (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′))
      (apᵈ-p (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′)) (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′)) ∙ᵈ
    dep (sym (p-⁺ᵀ* (γ*₁ ⁺^*))) ∙ᵈ
    apᵈ-[]ᵀ* (ap-++ (sym (▹-[]ᵀˡ* γ*₁))) (symᵈ (⁺^*-▹ γ*₁))
    where
    Ω▹A-γ*₀₁′ :
      (Ω [ γ*₀ ]ᵀˡ* ▹ A [ γ*₀ ⁺^* ]ᵀ*) ≡ (Ω [ γ*₁ ]ᵀˡ* ▹ A [ γ*₁ ⁺^* ]ᵀ*)
    Ω▹A-γ*₀₁′ = sym (▹-[]ᵀˡ* γ*₀) ∙ Ω▹A-γ*₀₁ ∙ ▹-[]ᵀˡ* γ*₁
  []ᵀ→⁺^ᵀᵛ {Ω = Ω ▹ A} Ω▹A-γ*₀₁ (vs bᵛ) =
    apᵈ-[]ᵀ* (ap-++ (▹-[]ᵀˡ* γ*₀)) (⁺^*-▹ γ*₀) ∙ᵈ
    dep (p-⁺ᵀ* (γ*₀ ⁺^*)) ∙ᵈ
    apᵈ-[]ᵀ
      (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′))
      ([]ᵀ→⁺^ᵀᵛ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′) bᵛ)
      (ap-▹ (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′)) (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′))
      (apᵈ-p (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′)) (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′)) ∙ᵈ
    dep (sym (p-⁺ᵀ* (γ*₁ ⁺^*))) ∙ᵈ
    apᵈ-[]ᵀ* (ap-++ (sym (▹-[]ᵀˡ* γ*₁))) (symᵈ (⁺^*-▹ γ*₁))
    where
    Ω▹A-γ*₀₁′ :
      (Ω [ γ*₀ ]ᵀˡ* ▹ A [ γ*₀ ⁺^* ]ᵀ*) ≡ (Ω [ γ*₁ ]ᵀˡ* ▹ A [ γ*₁ ⁺^* ]ᵀ*)
    Ω▹A-γ*₀₁′ = sym (▹-[]ᵀˡ* γ*₀) ∙ Ω▹A-γ*₀₁ ∙ ▹-[]ᵀˡ* γ*₁

module []ᵛ→⁺^
  (γ*₀ γ*₁ : Sub* Δ Γ)
  (γ*₀₁ᵀ : ∀ {i} {A : Ty Γ i} → A [ γ*₀ ]ᵀ* ≡ A [ γ*₁ ]ᵀ*)
  (γ*₀₁ᵛ :
    ∀ {i} {A : Ty Γ i} {a}
    (aᵛ : Var Γ A a) → a [ γ*₀ ]ᵗ* ≡[ ap-Tm γ*₀₁ᵀ ] a [ γ*₁ ]ᵗ*)
  where
  private
    []ᵛ→⁺^ᵛ₀ :
      (Ω-γ*₀₁ : Ω [ γ*₀ ]ᵀˡ* ≡ Ω [ γ*₁ ]ᵀˡ*) (aᵛ : Var (Γ ++ Ω) A a) →
      a [ γ*₀ ⁺^* ]ᵗ*
        ≡[ ap-Tm₂ (ap-++ Ω-γ*₀₁) ([]ᵀ→⁺^ᵀᵛ γ*₀ γ*₁ γ*₀₁ᵀ Ω-γ*₀₁ aᵛ) ]
      a [ γ*₁ ⁺^* ]ᵗ*
    []ᵛ→⁺^ᵛ₀ {Ω = ◇} ◇-γ*₀₁ aᵛ =
      apᵈ-[]ᵗ*ᵣ (ap-++ (◇-[]ᵀˡ* γ*₀)) (⁺^*-◇ γ*₀) ∙ᵈ
      γ*₀₁ᵛ aᵛ ∙ᵈ
      apᵈ-[]ᵗ*ᵣ (ap-++ (sym (◇-[]ᵀˡ* γ*₁))) (symᵈ (⁺^*-◇ γ*₁))
    []ᵛ→⁺^ᵛ₀ {Ω = Ω ▹ A} Ω▹A-γ*₀₁ vz =
      apᵈ-[]ᵗ*ᵣ (ap-++ (▹-[]ᵀˡ* γ*₀)) (⁺^*-▹ γ*₀) ∙ᵈ
      q-⁺* (γ*₀ ⁺^*) ∙ᵈ
      apᵈ-q (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′)) (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′) ∙ᵈ
      symᵈ (q-⁺* (γ*₁ ⁺^*)) ∙ᵈ
      apᵈ-[]ᵗ*ᵣ (ap-++ (sym (▹-[]ᵀˡ* γ*₁))) (symᵈ (⁺^*-▹ γ*₁))
      where
      Ω▹A-γ*₀₁′ :
        (Ω [ γ*₀ ]ᵀˡ* ▹ A [ γ*₀ ⁺^* ]ᵀ*) ≡ (Ω [ γ*₁ ]ᵀˡ* ▹ A [ γ*₁ ⁺^* ]ᵀ*)
      Ω▹A-γ*₀₁′ = sym (▹-[]ᵀˡ* γ*₀) ∙ Ω▹A-γ*₀₁ ∙ ▹-[]ᵀˡ* γ*₁
    []ᵛ→⁺^ᵛ₀ {Ω = Ω ▹ A} Ω▹A-γ*₀₁ (vs bᵛ) =
      apᵈ-[]ᵗ*ᵣ (ap-++ (▹-[]ᵀˡ* γ*₀)) (⁺^*-▹ γ*₀) ∙ᵈ
      p-⁺ᵗ* (γ*₀ ⁺^*) ∙ᵈ
      apᵈ-[]ᵗ₅
        (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′))
        ([]ᵀ→⁺^ᵀᵛ γ*₀ γ*₁ γ*₀₁ᵀ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′) bᵛ)
        ([]ᵛ→⁺^ᵛ₀ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′) bᵛ)
        (ap-▹ (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′)) (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′))
        (apᵈ-p (ap-++ (▹ᵀˡ-inj-Ω Ω▹A-γ*₀₁′)) (▹ᵀˡ-inj-A Ω▹A-γ*₀₁′)) ∙ᵈ
      symᵈ (p-⁺ᵗ* (γ*₁ ⁺^*)) ∙ᵈ
      apᵈ-[]ᵗ*ᵣ (ap-++ (sym (▹-[]ᵀˡ* γ*₁))) (symᵈ (⁺^*-▹ γ*₁))
      where
      Ω▹A-γ*₀₁′ :
        (Ω [ γ*₀ ]ᵀˡ* ▹ A [ γ*₀ ⁺^* ]ᵀ*) ≡ (Ω [ γ*₁ ]ᵀˡ* ▹ A [ γ*₁ ⁺^* ]ᵀ*)
      Ω▹A-γ*₀₁′ = sym (▹-[]ᵀˡ* γ*₀) ∙ Ω▹A-γ*₀₁ ∙ ▹-[]ᵀˡ* γ*₁

    []ᵛ→⁺^ᵀ₀ :
      (Ω-γ*₀₁ : Ω [ γ*₀ ]ᵀˡ* ≡ Ω [ γ*₁ ]ᵀˡ*) → NTy (Γ ++ Ω) i A →
      A [ γ*₀ ⁺^* ]ᵀ* ≡[ ap-Ty (ap-++ Ω-γ*₀₁) ] A [ γ*₁ ⁺^* ]ᵀ*
    []ᵛ→⁺^ᵀᵗ₀ :
      (Ω-γ*₀₁ : Ω [ γ*₀ ]ᵀˡ* ≡ Ω [ γ*₁ ]ᵀˡ*) → NTm (Γ ++ Ω) A a →
      A [ γ*₀ ⁺^* ]ᵀ* ≡[ ap-Ty (ap-++ Ω-γ*₀₁) ] A [ γ*₁ ⁺^* ]ᵀ*
    []ᵛ→⁺^ᵗ₀ :
      (Ω-γ*₀₁ : Ω [ γ*₀ ]ᵀˡ* ≡ Ω [ γ*₁ ]ᵀˡ*) (aᴺ : NTm (Γ ++ Ω) A a) →
      a [ γ*₀ ⁺^* ]ᵗ* ≡[ ap-Tm₂ (ap-++ Ω-γ*₀₁) ([]ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ aᴺ) ]
      a [ γ*₁ ⁺^* ]ᵗ*

    []ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ (Uᴺ i) =
      dep (U-[]* (γ*₀ ⁺^*)) ∙ᵈ
      apᵈ-U (ap-++ Ω-γ*₀₁) ∙ᵈ
      dep (sym (U-[]* (γ*₁ ⁺^*)))
    []ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ (Elᴺ αᴺ) =
      dep (El-[]* (γ*₀ ⁺^*)) ∙ᵈ
      apᵈ-El (ap-++ Ω-γ*₀₁) (splitlr ([]ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ αᴺ)) ∙ᵈ
      dep (sym (El-[]* (γ*₁ ⁺^*)))
    []ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ (Πᴺ Aᴺ Bᴺ) =
      dep (Π-[]* (γ*₀ ⁺^*)) ∙ᵈ
      apᵈ-Π
        (ap-++ Ω-γ*₀₁)
        ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ)
        ( apᵈ-[]ᵀ* (ap-++ (sym (▹-[]ᵀˡ* γ*₀))) (symᵈ (⁺^*-▹ γ*₀)) ∙ᵈ
          []ᵛ→⁺^ᵀ₀
            ( ▹-[]ᵀˡ* γ*₀ ∙
              ap-▹ᵀˡ Ω-γ*₀₁ ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ) ∙
              sym (▹-[]ᵀˡ* γ*₁))
            Bᴺ ∙ᵈ
          apᵈ-[]ᵀ* (ap-++ (▹-[]ᵀˡ* γ*₁)) (⁺^*-▹ γ*₁)) ∙ᵈ
      dep (sym (Π-[]* (γ*₁ ⁺^*)))

    []ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ (var aᵛ) = []ᵀ→⁺^ᵀᵛ γ*₀ γ*₁ γ*₀₁ᵀ Ω-γ*₀₁ aᵛ
    []ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ (cᴺ Aᴺ) =
      dep (U-[]* (γ*₀ ⁺^*)) ∙ᵈ
      apᵈ-U (ap-++ Ω-γ*₀₁) ∙ᵈ
      dep (sym (U-[]* (γ*₁ ⁺^*)))

    []ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ (appᴺ _ Bᴺ fᴺ aᴺ) =
      dep (⟨⟩-[]ᵀ* (γ*₀ ⁺^*)) ∙ᵈ
      apᵈ-[]ᵀ
        (ap-▹ (ap-++ Ω-γ*₀₁) ([]ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ aᴺ))
        ( apᵈ-[]ᵀ* (ap-++ (sym (▹-[]ᵀˡ* γ*₀))) (symᵈ (⁺^*-▹ γ*₀)) ∙ᵈ
          []ᵛ→⁺^ᵀ₀
            ( ▹-[]ᵀˡ* γ*₀ ∙
              ap-▹ᵀˡ Ω-γ*₀₁ ([]ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ aᴺ) ∙
              sym (▹-[]ᵀˡ* γ*₁))
            Bᴺ ∙ᵈ
          apᵈ-[]ᵀ* (ap-++ (▹-[]ᵀˡ* γ*₁)) (⁺^*-▹ γ*₁))
        (ap-++ Ω-γ*₀₁)
        (apᵈ-⟨⟩ (ap-++ Ω-γ*₀₁) ([]ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ aᴺ) ([]ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ aᴺ)) ∙ᵈ
      dep (sym (⟨⟩-[]ᵀ* (γ*₁ ⁺^*)))
    []ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ (lamᴺ Aᴺ _ bᴺ) =
      dep (Π-[]* (γ*₀ ⁺^*)) ∙ᵈ
      apᵈ-Π
        (ap-++ Ω-γ*₀₁)
        ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ)
        ( apᵈ-[]ᵀ* (ap-++ (sym (▹-[]ᵀˡ* γ*₀))) (symᵈ (⁺^*-▹ γ*₀)) ∙ᵈ
          []ᵛ→⁺^ᵀᵗ₀
            ( ▹-[]ᵀˡ* γ*₀ ∙
              ap-▹ᵀˡ Ω-γ*₀₁ ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ) ∙
              sym (▹-[]ᵀˡ* γ*₁))
            bᴺ ∙ᵈ
          apᵈ-[]ᵀ* (ap-++ (▹-[]ᵀˡ* γ*₁)) (⁺^*-▹ γ*₁)) ∙ᵈ
      dep (sym (Π-[]* (γ*₁ ⁺^*)))

    []ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ (var aᵛ) = []ᵛ→⁺^ᵛ₀ Ω-γ*₀₁ aᵛ
    []ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ (cᴺ Aᴺ) =
      c-[]* (γ*₀ ⁺^*) ∙ᵈ
      apᵈ-c (ap-++ Ω-γ*₀₁) ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ) ∙ᵈ
      symᵈ (c-[]* (γ*₁ ⁺^*))

    []ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ (appᴺ _ Bᴺ fᴺ aᴺ) =
      app-[]* (γ*₀ ⁺^*) ∙ᵈ
      apᵈ-app
        (ap-++ Ω-γ*₀₁)
        ([]ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ aᴺ)
        ( apᵈ-[]ᵀ* (ap-++ (sym (▹-[]ᵀˡ* γ*₀))) (symᵈ (⁺^*-▹ γ*₀)) ∙ᵈ
          []ᵛ→⁺^ᵀ₀
            ( ▹-[]ᵀˡ* γ*₀ ∙
              ap-▹ᵀˡ Ω-γ*₀₁ ([]ᵛ→⁺^ᵀᵗ₀ Ω-γ*₀₁ aᴺ) ∙
              sym (▹-[]ᵀˡ* γ*₁))
            Bᴺ ∙ᵈ
          apᵈ-[]ᵀ* (ap-++ (▹-[]ᵀˡ* γ*₁)) (⁺^*-▹ γ*₁))
        (splitlr ([]ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ fᴺ))
        ([]ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ aᴺ) ∙ᵈ
      symᵈ (app-[]* (γ*₁ ⁺^*))
    []ᵛ→⁺^ᵗ₀ Ω-γ*₀₁ (lamᴺ Aᴺ _ bᴺ) =
      lam-[]* (γ*₀ ⁺^*) ∙ᵈ
      apᵈ-lam
        (ap-++ Ω-γ*₀₁)
        ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ)
        ( apᵈ-[]ᵀ* (ap-++ (sym (▹-[]ᵀˡ* γ*₀))) (symᵈ (⁺^*-▹ γ*₀)) ∙ᵈ
          []ᵛ→⁺^ᵀᵗ₀
            ( ▹-[]ᵀˡ* γ*₀ ∙
              ap-▹ᵀˡ Ω-γ*₀₁ ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ) ∙
              sym (▹-[]ᵀˡ* γ*₁))
            bᴺ ∙ᵈ
          apᵈ-[]ᵀ* (ap-++ (▹-[]ᵀˡ* γ*₁)) (⁺^*-▹ γ*₁))
        ( apᵈ-[]ᵗ*ᵣ (ap-++ (sym (▹-[]ᵀˡ* γ*₀))) (symᵈ (⁺^*-▹ γ*₀)) ∙ᵈ
          []ᵛ→⁺^ᵗ₀
            ( ▹-[]ᵀˡ* γ*₀ ∙
              ap-▹ᵀˡ Ω-γ*₀₁ ([]ᵛ→⁺^ᵀ₀ Ω-γ*₀₁ Aᴺ) ∙
              sym (▹-[]ᵀˡ* γ*₁))
            bᴺ ∙ᵈ
          apᵈ-[]ᵗ*ᵣ (ap-++ (▹-[]ᵀˡ* γ*₁)) (⁺^*-▹ γ*₁)) ∙ᵈ
      symᵈ (lam-[]* (γ*₁ ⁺^*))

  []ᵛ→[]ᵀˡ : Ω [ γ*₀ ]ᵀˡ* ≡ Ω [ γ*₁ ]ᵀˡ*
  []ᵛ→[]ᵀˡ {Ω = ◇} = ◇-[]ᵀˡ* γ*₀ ∙ sym (◇-[]ᵀˡ* γ*₁)
  []ᵛ→[]ᵀˡ {Ω = Ω ▹ A} =
    ▹-[]ᵀˡ* γ*₀ ∙
    ap-▹ᵀˡ []ᵛ→[]ᵀˡ ([]ᵛ→⁺^ᵀ₀ []ᵛ→[]ᵀˡ (normᵀ A)) ∙
    sym (▹-[]ᵀˡ* γ*₁)

  []ᵛ→⁺^ᵀ : A [ γ*₀ ⁺^* ]ᵀ* ≡[ ap-Ty (ap-++ []ᵛ→[]ᵀˡ) ] A [ γ*₁ ⁺^* ]ᵀ*
  []ᵛ→⁺^ᵀ {A} = []ᵛ→⁺^ᵀ₀ []ᵛ→[]ᵀˡ (normᵀ A)

  []ᵛ→⁺^ᵗ :
    a [ γ*₀ ⁺^* ]ᵗ* ≡[ ap-Tm₂ (ap-++ []ᵛ→[]ᵀˡ) []ᵛ→⁺^ᵀ ] a [ γ*₁ ⁺^* ]ᵗ*
  []ᵛ→⁺^ᵗ {a} = []ᵛ→⁺^ᵗ₀ []ᵛ→[]ᵀˡ (normᵗ a)

module _ {A : Ty Γ i} {γ : Sub Δ Γ} where
  open []ᵛ→⁺^ (⌜ p ⌝ ∘ ⌜ γ ⁺ ⌝) (⌜ γ ⌝ ∘ ⌜ p ⌝) p-⁺ᵀ (λ _ → p-⁺ᵗ {A = A}) public
    renaming ([]ᵛ→[]ᵀˡ to p-⁺ᵀˡ; []ᵛ→⁺^ᵀ to p-⁺ᵀ-⁺^; []ᵛ→⁺^ᵗ to p-⁺ᵗ-⁺^)

module _ {a : Tm Γ A} where
  open []ᵛ→⁺^ (⌜ p ⌝ ∘ ⌜ ⟨ a ⟩ ⌝) id p-⟨⟩ᵀ (λ _ → p-⟨⟩ᵗ) public
    renaming ([]ᵛ→[]ᵀˡ to p-⟨⟩ᵀˡ; []ᵛ→⁺^ᵀ to p-⟨⟩ᵀ-⁺^; []ᵛ→⁺^ᵗ to p-⟨⟩ᵗ-⁺^)

module _ {a : Tm Γ A} {γ : Sub Δ Γ} where
  ⟨⟩-[]ᵛ :
    Var (Γ ▹ A) B b →
    b [ ⟨ a ⟩ ]ᵗ [ γ ]ᵗ ≡[ ap-Tm ⟨⟩-[]ᵀ ] b [ γ ⁺ ]ᵗ [ ⟨ a [ γ ]ᵗ ⟩ ]ᵗ
  ⟨⟩-[]ᵛ vz =
    apᵈ-[]ᵗ p-⟨⟩ᵀ q-⟨⟩ ∙ᵈ symᵈ q-⟨⟩ ∙ᵈ apᵈ-[]ᵗ (sym p-⁺ᵀ) (symᵈ q-⁺)
  ⟨⟩-[]ᵛ (vs cᵛ) =
    apᵈ-[]ᵗ p-⟨⟩ᵀ p-⟨⟩ᵗ ∙ᵈ symᵈ p-⟨⟩ᵗ ∙ᵈ apᵈ-[]ᵗ (sym p-⁺ᵀ) (symᵈ p-⁺ᵗ)

  open []ᵛ→⁺^ (⌜ ⟨ a ⟩ ⌝ ∘ ⌜ γ ⌝) (⌜ γ ⁺ ⌝ ∘ ⌜ ⟨ a [ γ ]ᵗ ⟩ ⌝) ⟨⟩-[]ᵀ ⟨⟩-[]ᵛ
    public
    renaming ([]ᵛ→[]ᵀˡ to ⟨⟩-[]ᵀˡ; []ᵛ→⁺^ᵀ to ⟨⟩-[]ᵀ-⁺^; []ᵛ→⁺^ᵗ to ⟨⟩-[]ᵗ-⁺^)

module _ {A : Ty Γ i} where
  ▹-ηᵛ : Var (Γ ▹ A) B b → b ≡[ ap-Tm ▹-ηᵀ ] b [ p ⁺ ]ᵗ [ ⟨ q ⟩ ]ᵗ
  ▹-ηᵛ vz = symᵈ q-⟨⟩ ∙ᵈ apᵈ-[]ᵗ (sym p-⁺ᵀ) (symᵈ q-⁺)
  ▹-ηᵛ (vs cᵛ) = symᵈ p-⟨⟩ᵗ ∙ᵈ apᵈ-[]ᵗ (sym p-⁺ᵀ) (symᵈ p-⁺ᵗ)

  open []ᵛ→⁺^ id (⌜ p ⁺ ⌝ ∘ ⌜ ⟨ q ⟩ ⌝) ▹-ηᵀ ▹-ηᵛ public
    renaming ([]ᵛ→[]ᵀˡ to ▹-ηᵀˡ; []ᵛ→⁺^ᵀ to ▹-ηᵀ-⁺^; []ᵛ→⁺^ᵗ to ▹-ηᵗ-⁺^)

-- -} -- -} -- -} -- -} -- -} -- -} -- -} -- -} -- -} -- -} -- -} -- -}
